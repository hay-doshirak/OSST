@ECHO OFF

REM Проверка наличия параметров
IF "%~1"=="" GOTO ERROR_FILE
IF "%~2"=="" GOTO ERROR_PARAMETR
IF "%~3"=="" GOTO ERROR_PARAMETR
IF "%~4"=="" GOTO ERROR_PARAMETR

REM проверка существования исходного файла
IF NOT EXIST "%~1" (
    ECHO ERROR: File "%~1" not found!
    GOTO END
)	

REM проверка допустимых значений параметров
SET /A lines1=%~2
SET /A lines2=%~3
SET /A lines3=%~4

IF %lines1% LEQ 0 (
    ECHO ERROR: Parameter 2 must be a positive number.
    GOTO END
)	
IF %lines2% LEQ 0 (
    ECHO ERROR: Parameter 3 must be a positive number.
    GOTO END
)	
IF %lines3% LEQ 0 (
    ECHO ERROR: Parameter 4 must be a positive number.
    GOTO END
)	

REM объявление переменных с размерами 3-х будущих файлов
SET /A part1=%~2
SET /A part2=%~3
SET /A part3=%~4

SETLOCAL ENABLEDELAYEDEXPANSION
SET line_counter=0

ECHO Writing 1.txt...
(FOR /F "usebackq DELIMS=" %%i IN ("%~1") DO (
    SET /A line_counter+=1
    IF !line_counter! LEQ !part1! ECHO %%i
)) > 1.txt

ECHO Writing 2.txt...
SET line_counter=0
(FOR /F "usebackq SKIP=%part1% DELIMS=" %%i IN ("%~1") DO (
    SET /A line_counter+=1
    IF !line_counter! LEQ !part2! ECHO %%i
)) > 2.txt

ECHO Writing 3.txt...
SET line_counter=0
SET /A skip=%part1%+%part2%
(FOR /F "usebackq SKIP=%skip% DELIMS=" %%i IN ("%~1") DO (
    SET /A line_counter+=1
    IF !line_counter! LEQ !part3! ECHO %%i
)) > 3.txt

ECHO DONE!
GOTO END

REM ошибки
:ERROR_FILE
ECHO ERROR: Enter file name
GOTO END

:ERROR_PARAMETR
ECHO ERROR: Enter parametrs
GOTO END

:END
PAUSE